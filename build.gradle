buildscript {
    ext {
        projectName = 'walking-be'
        projectVersion = '1.0.0'
        springBootVersion = '2.7.5'
        dependencyManagementVersion = '1.0.15.RELEASE'

        // jwt
        jsonwebtokenVersion = '0.11.5'

        // jsr
        jsr305Version = '3.0.1'

        // swagger & restdocs
        asciidoctorVersion = '3.3.2'
        epagesRestDocsApiSpecVersion = '0.16.0'
        hidetakeSwaggerGeneratorVersion = '2.18.2'
        springDocOpenApiUiVersion = '1.6.9'
        swaggerUIVersion = '4.1.3'
        // spotless
        spotlessVersion = '6.8.0'

        // spring cloud
        set('springCloudVersion', "2021.0.1")
    }
}


plugins {
    id 'java'

    // spring
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}"

    // code quality
    id "com.diffplug.spotless" version "${spotlessVersion}"

    // docs generation
    id 'org.asciidoctor.jvm.convert' version "${asciidoctorVersion}"
    id 'com.epages.restdocs-api-spec' version "${epagesRestDocsApiSpecVersion}"
    id 'org.hidetake.swagger.generator' version "${hidetakeSwaggerGeneratorVersion}"

    // test coverage
    id 'jacoco'
}

apply from: './tasks/install-git-hooks.gradle'
apply from: './tasks/set-git-commit-template.gradle'

allprojects {
    apply plugin: 'com.diffplug.spotless'
    apply from: rootProject.rootDir.toString() + '/tasks/formatting-task.gradle'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.hidetake.swagger.generator'

    group = 'com.walking'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    allprojects {}

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // database
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
        api 'mysql:mysql-connector-java'

        api 'org.hibernate:hibernate-envers'

        api 'org.hibernate.validator:hibernate-validator'
        api 'javax.annotation:javax.annotation-api'

        annotationProcessor("javax.persistence:javax.persistence-api")

        // jackson
        api 'com.fasterxml.jackson.core:jackson-databind'
        api 'com.fasterxml.jackson.core:jackson-core'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
        api group: 'com.google.code.findbugs', name: 'jsr305', version: "${jsr305Version}"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
